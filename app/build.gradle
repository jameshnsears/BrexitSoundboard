apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../jacoco.gradle'
apply from: '../coveralls.gradle'
apply from: '../sonarqube.gradle'


task espresso(type: Exec) {
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'window_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '0'
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'na.brexitsoundboard'
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 23
        versionName '1.1.2'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
        warningsAsErrors false
        checkAllWarnings = true
        xmlReport true
        htmlReport true
    }

    buildTypes {
        def appcenterKeyProd = System.getenv("APPCENTER_KEY_PROD_BREXITSOUNDBOARD")
        if (appcenterKeyProd == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyProd = properties.getProperty('APPCENTER_KEY_PROD_BREXITSOUNDBOARD')
        }

        def appcenterKeyDev = System.getenv("APPCENTER_KEY_DEV_BREXITSOUNDBOARD")
        if (appcenterKeyDev == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyDev = properties.getProperty('APPCENTER_KEY_DEV_BREXITSOUNDBOARD')
        }

        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }

        release {
            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyDev\"")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            animationsDisabled = true
            all {
               jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }
}

dependencies {
    def appCenterSdkVersion = '3.2.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation "io.mockk:mockk-android:1.10.0"
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation 'org.mockito:mockito-core:3.4.6'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.work:work-runtime-ktx:2.4.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation 'commons-io:commons-io:2.6'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}

repositories {
    jcenter()
    mavenCentral()
}
